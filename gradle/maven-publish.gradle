apply plugin: 'maven-publish'

def pomFile = file("gradle/pom.groovy")
assert pomFile.exists()
def slurper = new ConfigSlurper()
slurper.setBinding(["projectName": project.name])
def config = slurper.parse(pomFile.toURI().toURL())


publishing {
    publications {
        mavenJava(MavenPublication) {

            pom.withXml {
                def rootNode = asNode()
                config.keySet().each { key ->
                    addNode(config, key, rootNode)
                }
            }
            from components.java
        }
    }
}

private void addNode(config, key, Node node) {
    if (!(config[key] instanceof Map)) {
        node.appendNode(key, config[key])
    }
    else {
        Node newNode = new Node(node,key)
        config[key].keySet().each{
            addNode(config[key],it,newNode)
        }
    }
}
